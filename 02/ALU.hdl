// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //// Replace this comment with your code.

    Not16(in=x, out=xn);
    Mux4Way16(a=x, b=false, c=xn, d=true, sel[0]=zx, sel[1]=nx, out=xx);

    Not16(in=y, out=yn);
    Mux4Way16(a=y, b=false, c=yn, d=true, sel[0]=zy, sel[1]=ny, out=yy);

    And16(a=xx, b=yy, out=xay);
    Add16(a=xx, b=yy, out=xpy);
    Mux16(a=xay, b=xpy, sel=f, out=xy);

    Not16(in=xy, out=nxy);
    Mux16(a=xy, b=nxy, sel=no, out=out, out[0..7]=o1, out[8..15]=o2, out[15]=ng);

    Or8Way(in=o1, out=oz1);
    Or8Way(in=o2, out=oz2);
    Or(a=oz1, b=oz2, out=oz);
    Not(in=oz, out=zr);


}